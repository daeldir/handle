handle
======

A CoffeeScript literate programming project manager

What is it?
-----------

`handle` is a utility whose goal is to help a special programming
practice in web development.

The requirements are the following:

 - We should be able to write our website in only one language:
   CoffeeScript. To this end, we can use CoffeeScript templates engines
   like
   [CoffeeTemplates](https://github.com/mikesmullin/coffee-templates)
   for HTML and
   [CoffeeStylesheets](https://github.com/mikesmullin/coffee-stylesheets)
   for CSS.
 - We should write our application with the new feature of literate
   programming of CoffeeScript.
 - We should be able to access the documentation of our code in HTML.
 - We should be able to write our code for the client the same way we
   write our code for the server (with CommonJS modules).
 - The compilation of HTML, javascript and CSS should be done once when
   starting the application, when possible.
 - Saving a file and reloading a page in the browser should work like
   the old "traditionnal" when development techniques, that is, the
   changes are visible without server restart.
 - The script should run the tests of our project at compilation time.

Usage
-----

**handle** _command_

 * handle compile: compile the server.
 * handle clean: remove all the files generated by handle.
 * TODO: handle watch: compile and start the server, and do it again at
   each file change.
 * TODO: handle run: compile and start the server.
 * TODO: handle safe-run: compile and start the server, and start it
   again if the server crash.
 * TODO: handle bundle: compile the server and place all generated files
   in a directory, which can be started as a standalone application,
   without having to fetch the dependencies.
 * TODO: handle test: Only test the server, don't start it for browser
   access.
 * TODO: handle coverage: run tests and report code coverage of the
   tests.
 * TODO: handle completude: run feature tests and report the project
   advancement state.

How does it works?
------------------

The file you are currently reading is the implementation of `handle`,
written in literate CoffeeScript. Now that we explained what the program
should do, let's get into the code!

### Dependencies

We depend on many libraries. We will need `coffee-templates` to compile
our coffeescript templates into HTML, and `coffee-stylesheets` to
compile into CSS. We will also need `browserify` to generate a
javascript bundle loadable in the browser and coded with CommonJS module
system.

```coffeescript
    templates = require 'coffee-templates'
    stylesheets = require 'coffee-stylesheets'
    browserify = require 'browserify'
```

We also need to load the CoffeeScript library to compile the project
code into Javascript:

```coffeescript
    coffee = require 'coffee-script'
```

We will read files to compile the source code and walk the tree
directory. We also will be manipulating file paths.

```coffeescript
    fs = require 'fs'
    path = require 'path'
```

### Compilation

All of our code goes into the `src/` directory. As _usual_, we compile
the content of that directory into the `lib/` directory. An extra step
will be taken after that to make the `client.coffee.md` code and all
it's dependencies attainable by the web browser.

#### Compiling all the CoffeeScript

We begin by compiling all the CoffeeScripts files to Javascript. The
client-side bundle generation by browserify will come later, using the
Javascript version of our code.

First, we compile all the files from `./src` to `./lib`, creating
`./lib` if it does not exist.

```coffeescript
    srcToLib = ->
      return unless fs.existsSync 'src'
      fs.mkdirSync 'lib' unless fs.existsSync 'lib'
      walkTree 'src', 'top-down',
        dir: (dir) ->
          newPath = dir.replace('src/', 'lib/')
          fs.mkdirSync newPath unless fs.existsSync newPath
        file: (file) ->
          code = fs.readFileSync(file).toString()
          newPath = file
            .replace('src/', 'lib/')
            .replace('.coffee.md', '.js')
          js = coffee.compile code, literate: yes
          fs.writeFileSync newPath, js
```

#### Compiling the browser side code

We will place some code into a cache. We need to create it if it does
not exists. We also create the `js` directory, where `client.js` will
land, if it does not exist. Then, we load ou lib into `browserify`, and
that module will do all the work for us.

Writing a bundle with `browserify` is not really practical. I remember a
time when `client.bundle()` would have returned a string. Or maybe did I
dream? Anyway, we make the bundle, which return a `Stream`, which we
pipe into a file stream, which we create with the `fs` module. All text
sent to this stream will be written in `cache/js/client.js`. Because 
`fs.writeFileSync 'cache/js/client.js', client.bundle()` would have been
too straightforward...

```coffeescript
    commonJsToBrowser = ->
      fs.mkdirSync 'cache' unless fs.existsSync 'cache'
      fs.mkdirSync 'cache/js' unless fs.existsSync 'cache/js'
      client = browserify './lib/client.js'
      client.bundle().pipe fs.createWriteStream 'cache/js/client.js'
```

### Cleaning

`handle clean` will remove all the files generated by handle. Those are,
hopefully, all in reserved directories: `cache`, and `lib` for now.

```coffeescript
    clean = ->
      removeActions =
        dir: (path) -> fs.rmdirSync path
        file: (path) -> fs.unlinkSync path
      walkTree 'lib', 'bottom-up', removeActions
      walkTree 'cache', 'bottom-up', removeActions
```

### Utility functions

#### walkTree

The `walkTree` function recursively read all the files in a directory.
It will call a function for each directory it traverses, and for each
file it encounters. Depending on what we want to do, we may prefer to do
an action on a directory before it's content (when doing a copy, for
example), or after (e.g. when deleting). So, `walkTree` take three
arguments: 

 * The name of the directory we have to walk through.
 * The order of execution, top-down being directory processed first,
   bottom-up being content processed first.
 * The actions to take, as an object. `dir` contains the action to take
   when processing a directory, and `file` contains the action to take
   on each file.

By default, we walk through the current directory, processing
directories before their content, and doing nothing.

```coffeescript
    walkTree = (name = './', order = 'top-down', {dir, file}) ->
      dir ?= (->); file ?= (->)
      dir name if order is 'top-down'
      files = fs.readdirSync name
      for f in files
        filePath = path.join name, f
        if fs.statSync(filePath).isDirectory()
          walkTree filePath, order, dir: dir, file: file
        else
          file filePath
      dir name if order is 'bottom-up'
```

### Taking the actions

Well, we only defined functions. If we wrote `handle compile` on the
command line, it would do nothing. Let's remedy that.

Note that we count three arguments (from zero). Argument zero is
`coffee`, it is the program called. Argument one is the name of our
handle script. Argument two is the one that will contain the wanted
command (`compile`, `run`, `watch`...).

```coffeescript
    if process.argv[2]? then switch process.argv[2]
      when 'compile'
        srcToLib()
        commonJsToBrowser()
      when 'clean'
        clean()
```


